buildscript {
    ext {
        kotlinVersion = '1.0.6'
        springBootVersion = '1.5.1.RELEASE'
    }
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'

    def javaVersion = 1.8

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    }

    compileJava {
        //enable compilation in a separate daemon process
        options.fork = true
        //enable incremental compilation
        options.incremental = true
    }

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/main/")
        }
    }

}

ext.libraries = [
        spring_web             : 'org.springframework.boot:spring-boot-starter-web',
        spring_data_jpa        : 'org.springframework.boot:spring-boot-starter-data-jpa',
        spring_data_rest       : 'org.springframework.boot:spring-boot-starter-data-rest',
        spring_hal_browser     : 'org.springframework.data:spring-data-rest-hal-browser',
        spring_hateoas         : 'org.springframework.boot:spring-boot-starter-hateoas',
        spring_actuator        : 'org.springframework.boot:spring-boot-starter-actuator',

        spring_test            : 'org.springframework.boot:spring-boot-starter-test',

        liquibase              : 'org.liquibase:liquibase-core',
        mysql_jdbc             : 'mysql:mysql-connector-java',
]

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}

